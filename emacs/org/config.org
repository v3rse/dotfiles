#+TITLE: Config
#+PROPERTY: header-args emacs-lisp :tangle "~/dotfiles/emacs/.config/doom/config.el" :mkdirp yes

* INTRODUCTION
** Requirement
- [[https://github.com/plexus/chemacs2?tab=readme-ov-file#doom-emacs][Chemacs2]]
- [[https://github.com/doomemacs/doomemacs][Doom Emacs]]
- Fonts
  - ETBook/ETBembo
  - Iosevka SS04
- An =authinfo= file with all the secrets needed for:
  - gptel
  - usenet
  - forge

** Usage
Run =org-babel-tangle= using ~C-c C-v t~ to create the =config.el= file in the path specified in the headers of this file. This assumes the output file should be your =dotfiles= directory managed by =stow=.


* INITIALIZATION
** Lexical Binding?
#+begin_src emacs-lisp
;;; $DOOMDIR/config.el -*- lexical-binding: t; -*-
#+end_src

** Timezone
#+begin_src emacs-lisp
(setenv "TZ" "Europe/Berlin")
#+end_src

** Mac Specific
This removes the chunky title bar in the mac version fo emacs
#+begin_src emacs-lisp
(if (eq window-system 'mac)
        (add-hook 'doom-after-init-hook (lambda () (tool-bar-mode 1) (tool-bar-mode 0))))
#+end_src


* APPEARANCE
** General
#+begin_src emacs-lisp
(add-hook! 'visual-fill-column-mode-hook (setq display-line-numbers-mode -1))
(setq display-line-numbers-type t)
#+end_src

** Dashboard
#+begin_src emacs-lisp
(setq fancy-splash-image (concat doom-private-dir "splash.svg"))

(remove-hook '+doom-dashboard-functions #'doom-dashboard-widget-shortmenu)
(remove-hook '+doom-dashboard-functions #'doom-dashboard-widget-footer)

(add-hook! '+doom-dashboard-mode-hook (hl-line-mode -1))

(setq-hook! '+doom-dashboard-mode-hook evil-normal-state-cursor (list nil))
#+end_src

** Font
#+begin_src emacs-lisp
(setq doom-font (font-spec :family "Iosevka SS04" :size 15 :weight 'medium)
     doom-variable-pitch-font (font-spec :family "ETBembo" :size 20))
#+end_src

** Theme
#+begin_src emacs-lisp
(setq doom-theme 'doom-tomorrow-night)
#+end_src


* APPLICATIONS
** Programming
*** Project Management (Projectile)
#+begin_src emacs-lisp
(setq projectile-project-search-path '("~/src/personal" "~/src/other" "~/src/lab"))
#+end_src

*** Terminal (vterm)
#+begin_src emacs-lisp
(add-hook 'vterm-mode-hook #'goto-address-mode)
#+end_src

** Productivity
This is achieved via =org-mode=
*** General
#+begin_src emacs-lisp
(after! org
        (setq org-directory "~/org/"
                org-default-notes-file "~/org/gtd/inbox.org"
                org-agenda-files '("gtd/inbox.org" "gtd/agenda.org" "gtd/projects.org")
                org-ellipsis " ... "
                org-tags-column -80
                org-log-into-drawer t
                org-hide-emphasis-markers t
                org-agenda-start-day nil
                org-log-done 'time))
#+end_src

*** Modules
#+begin_src emacs-lisp
(after! org
  (add-to-list 'org-modules 'org-habit))
#+end_src

*** GTD
This is from the article [[https://vtimofeenko.com/posts/my-doom-emacs-setup/]["My doom-emacs setup"]]:
**** Capture Teamplates
These are templates for how I *capture* anything that comes to mind based the GTD methodology. Entries are differentiated either using keywords or tags.

  - Tasks: containing some action to be done
  - Notes: generally not actionable, or for future reference
  - Meetings: for which notes should be taken
  - Appointments: which are set sometime in the future

#+begin_src emacs-lisp
(after! org
        (setq org-capture-templates
        `(("t" "Task" entry (file+headline "gtd/inbox.org" "Tasks")
               ,(string-join '("* TODO %?"
                                ":PROPERTIES:"
                                ":CREATED: %U"
                                ":CATEGORY: Task"
                                ":END:")
                        "\n"))
        ("n" "Note" entry (file+headline "gtd/inbox.org" "Notes")
                ,(string-join '("* %?"
                                ":PROPERTIES:"
                                ":CREATED: %U"
                                ":CATEGORY: Note"
                                ":END:")
                        "\n"))
        ("m" "Meeting" entry (file+headline "gtd/inbox.org" "Meetings")
                ,(string-join '("* %? :MEETING"
                                "<%<%Y-%m-%d %a %H:00>>"
                                ""
                                "/Met with: /")
                        "\n"))
        ("a" "Appointment" entry (file+headline "gtd/inbox.org" "Appointments")
                ,(string-join '("* %? :APPOINTMENT:"
                                ":PROPERTIES:"
                                ":CREATED: %U"
                                ":CATEGORY: Appointment"
                                ":END:")
                        "\n"))
        ))
)
#+end_src

**** Org Task Keywords
Simplified list of task state keywords and their appearance:
 - PROJ : large tasks with many sub task that span a longer period
 - TODO : to be done in future
 - STRT : being done right now
 - HOLD : cannot proceed, waits for some external blocker
 - DONE : terminal state, task is done
 - CNCL : terminal state, task canceled

#+begin_src emacs-lisp
(after! org
        (setq org-todo-keywords '((sequence "TODO(t)" "NEXT(n)" "HOLD(h)" "PROJ(p)" "|" "DONE(d)" "CNCL(c)"))
        org-todo-keyword-faces '(("NEXT" . +org-todo-active)
                                ("HOLD" . +org-todo-onhold)
                                ("CNCL" . +org-todo-cancel)
                                ("PROJ" . +org-todo-project))))
#+end_src

**** Agenda Views
Add custom commands to =org-agenda=
#+begin_src emacs-lisp
(after! org-agenda
        (add-to-list 'org-agenda-custom-commands
        '("g" "Get Things Done (GTD)"
                ((agenda ""
                         ((org-agenda-span 'day)
                          (org-agenda-skip-function
                           '(org-agenda-skip-entry-if 'deadline))
                          (org-deadline-warning-days 0)))
                 (todo "TODO"
                        ((org-agenda-overriding-header "Refile Tasks")
                        (org-agenda-files '("gtd/inbox.org"))))
                (todo "TODO"
                      ((org-agenda-overriding-header "One-off Tasks")
                       (org-agenda-files '("gtd/agenda.org"))
                       (org-agenda-skip-function '(org-agenda-skip-entry-if 'deadline 'scheduled))))
                (todo "NEXT"
                        ((org-agenda-overriding-header "Follow-up Tasks")
                                (org-agenda-files '("gtd/someday-maybe.org"
                                                "gtd/projects.org"
                                                "gtd/agenda.org"))
                                (org-agenda-skip-function '(org-agenda-skip-entry-if 'deadline 'scheduled))))
                (todo "PROJ"
                        ((org-agenda-overriding-header "Projects")
                                (org-agenda-files '("gtd/projects.org"))))
                 (agenda nil
                         ((org-agenda-span 3)
                          (org-agenda-entry-types '(:deadline))
                          (org-deadline-warning-days 3)
                          (org-agenda-overriding-header "\nDeadlines\n")))
                 (tags "CLOSED>=\"<today>\""
                       ((org-agenda-overriding-header "\nCompleted today\n")))
                ))))
#+end_src

**** Moving Headings
Move things to the proper location.
In the end you should have an empty inbox.
***** Refile
#+begin_src emacs-lisp
(after! org
        (setq v3rse/org-refile-target-files '("gtd/agenda.org"
                                       "gtd/projects.org"
                                       "gtd/someday-maybe.org"
                                       "research/notes.org"))


        (setq v3rse/org-refile-file-paths
                        (let (result)
                                (dolist (file v3rse/org-refile-target-files result)
                                        (push (expand-file-name file org-directory) result))))

        (setq org-refile-targets
        '((nil :maxlevel . 9)
                (v3rse/org-refile-file-paths :maxlevel . 9))))
#+end_src

***** Archiving
Nothing to do here. Just using the default settings. All archives go to =<file>_archive=
*** Calendar/Diary
Use diary file in my synched folder
#+begin_src emacs-lisp
(setq diary-file "~/org/emacs-diary")
#+end_src

Setup org-agenda to use diary file
#+begin_src emacs-lisp
(after! org-agenda
  (setq org-agenda-include-diary t))
#+end_src

** Note Search (Deft)
#+begin_src emacs-lisp
(setq deft-directory "~/org"
      deft-extensions '("org")
      deft-recursive t)
#+end_src

** Epub Reader (Nov.el)
#+begin_src emacs-lisp
(use-package nov
  :config (add-to-list 'auto-mode-alist '("\\.epub\\'" . nov-mode)))
#+end_src

** Browser (eww)
#+begin_src emacs-lisp
(setq browse-url-browser-function 'eww-browse-url
      shr-indentation 2
      shr-width 70)

(set-popup-rule! "^\\*eww\\*" :side 'right :size 0.50)
#+end_src

** GPT (gptel)
#+begin_src emacs-lisp
(use-package! gptel
  :config
  (setq! gptel-default-mode 'org-mode)
  (setq! gptel-default-input-format 'org))
#+end_src

** RSS (elfeed)
#+begin_src emacs-lisp
(setq elfeed-feeds
        '(
          ;; News sites (using Ground News on Phone for now)
          ;; ("https://www.euronews.com/rss" news euronews)
          ("https://www.allsides.com/rss/news" news allsides)
          ;; Papers
          ;; ("https://rss.arxiv.org/rss/cs" papers arxiv-cs)
          ;; Blogs
          ("https://frontendmasters.com/blog/feed/" blog frontendmasters)
          ("https://sachachua.com/blog/feed/" blog sachachuaemacs)
          ;; Aggregators
          ("https://blaggregator.recurse.com/atom.xml?token=561d4f124fc342d78c6e25da65dfd69a" agg recurse)
          ("https://news.ycombinator.com/rss" agg hackernews)
          ("https://planet.emacslife.com/atom.xml" agg plantemacs)
          ("https://lobste.rs/rss" agg lobsters)
        )
)

(after! elfeed
  (setq elfeed-search-filter "@1-day-ago +unread"))
#+end_src

#+RESULTS:
: @1-day-ago +unread

** IRC (circe)
#+begin_src emacs-lisp
(after! circe
  (set-irc-server! "irc.libera.chat"
    `(:tls t
      :port 6697
      :nick "v3rse"
      :sasl-username "v3rse"
      :sasl-password "1amHappy"
      :channels ("#emacs" "#systemcrafters" "#org-mode"))))
#+end_src

** Usenet Reading (gnus)
#+begin_src emacs-lisp
(after! gnus
  (setq! gnus-select-method
      '(nntp "news.usenet.farm"
             (nntp-open-connection-function nntp-open-tls-stream)
             (nntp-port-number 563))))
#+end_src

** Mastodon
#+begin_src emacs-lisp
(after! mastodon
  (setq! mastodon-instance-url "https://recurse.social"
      mastodon-active-user "v3rse"))
#+end_src


* CUSTOM
** Reading Mode
#+begin_src emacs-lisp
(defvar-local v3rse/reading nil
    "This is set if reading visuals are active")

(defun v3rse/activate-reading ()
    "Makes text more book like and readable especially in org mode"
    (progn
        (variable-pitch-mode)
        (visual-fill-column-mode)
        (display-line-numbers-mode -1)
        (setq v3rse/reading t)
        (message "You're reading in style")))

(defun v3rse/deactivate-reading ()
    "Reverts org reading mode changes"
    (progn
        (variable-pitch-mode -1)
        (visual-fill-column-mode -1)
        (display-line-numbers-mode)
        (setq v3rse/reading nil)
        (message "Back to being a nerd")))

(defun v3rse/read ()
  "Makes text more book like and readable especially in org mode"
  (interactive)
  (if v3rse/reading
      (v3rse/deactivate-reading)
    (v3rse/activate-reading)))
#+end_src

** Bionic Reading
#+begin_src emacs-lisp
(defvar-local bionic-overlays nil
  "The overlays for bionicification in the current buffer.")

(defun bionic-word ()
  "Bionicify the word at point"
  (interactive)
  (let* ((bounds (bounds-of-thing-at-point 'word))
         (beg (car bounds))
         (end (cdr bounds))
         (whole-len (- end beg)))
    (cond
     ((>= whole-len 2)
      (let* ((half-len (/ whole-len 2))
             (real-len (if (or (> whole-len 6) (= whole-len 3))
                           (+ half-len 1)
                         half-len))
             (ov (make-overlay beg (+ beg real-len))))
        (overlay-put ov 'face 'bold)
        (push ov bionic-overlays)))
     ((> (- end beg) 1)
      (let ((ov (make-overlay beg (+ beg 1))))
        (overlay-put ov 'face 'bold)
        (push ov bionic-overlays)))
     (t nil))))

(defun bionic-buffer ()
  "Bionicify all the visible parts of the current buffer."
  (interactive)
  (if (not (null bionic-overlays))
      (bionic-debuffer))
  (save-excursion
    (goto-char (point-min))
    (while (not (= (point) (point-max)))
      (if (looking-at "\\w")
          (bionic-word))
      (forward-to-word 1))))

(defun bionic-debuffer ()
  "Undo the bionicification."
  (interactive)
  (dolist (ov bionic-overlays)
    (delete-overlay ov)))
#+end_src

** gptel models
#+begin_src emacs-lisp
(defun v3rse/gptel-use-claude ()
  "Switch to a claude backend for gptel"
  (interactive)
  (setq gptel-model 'claude-3-sonnet-20240229
        gptel-backend (gptel-make-anthropic "Claude"
                        :stream t
                        :key (cadr (auth-source-user-and-password "api.anthropic.com" "apikey")))))

(defun v3rse/gptel-use-chatgpt ()
  "Switch to a chatgpt backend for gptel"
  (interactive)
  (setq gptel-model (default-value 'gptel-model)
        gptel-backend (default-value 'gptel-backend)))
#+end_src
